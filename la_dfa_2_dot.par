
%start LaDfa2Dot
%title "LaDfa2Dot grammar"
%comment "Create a Graphviz representation of `parol`'s LookaheadDFAs"
%line_comment '//'

%%

LaDfa2Dot
    : NamingComment 'LookaheadDFA'^ Parts
    ;

Parts
    : '{'^ Prod0 Transitions K '}'^ CommaOpt^
    ;

Integer
    : /-?\d+/
    ;

NamingComment
    : "/\*"^ Integer '-'^ NtName "\*/"^
    ;

Prod0
    : 'prod0'^ ':'^ Integer ','^
    ;

TransitionsPrefix
    : 'transitions'^ ':'^ '&'^ '['^
    ;

TransitionsPostfix
    : ']'^ CommaOpt^
    ;

Transitions
    : TransitionsPrefix^ TransList TransitionsPostfix^
    ;

TransList
    : { TransEntry }
    ;

TransEntryPrefix
    : 'Trans'^ '('^
    ;

TransEntryPostfix
    : ')'^ ','^
    ;

TransEntryValue
    : Integer CommaOpt^
    ;

TransValues
    : TransEntryValue TransEntryValue TransEntryValue TransEntryValue
    ;

TransEntry
    : TransEntryPrefix^ TransValues TransEntryPostfix^
    ;

K
    : 'k'^ ':'^ Integer ','^
    ;

NtName
    : /"\w+?"/
    ;

CommaOpt
    : [ ','^ ]
    ;