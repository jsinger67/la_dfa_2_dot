
%start LaDfa2Dot
%title "LaDfa2Dot grammar"
%comment "Create a Graphviz representation of `parol`'s LookaheadDFAs"
%line_comment '//'

%%

LaDfa2Dot
    : NamingComment 'LookaheadDFA'^ Parts
    ;

Parts
    : '{'^ Prod0 Transitions K '}'^ [ ','^ ]
    ;

NamingComment
    : '/'^ '*'^ ProdNum Dash^ NtName '*'^ '/'^
    ;

Prod0
    : 'prod0'^ ':'^ Integer ','^
    ;

Transitions
    : 'transitions'^ ':'^ '&'^ '['^ TransList ']'^ [ ','^ ]
    ;

TransList
    : { TransEntry }
    ;

TransEntry
    : 'Trans'^ '('^ Integer ','^ Integer ','^ Integer ','^ Integer ')'^ ','^
    ;

K
    : 'k'^ ':'^ Integer ','^
    ;

ProdNum
    : Integer
    ;

NtName
    : /"\w+?"/
    ;

Integer
    : /-?\d+/
    ;

Dash
    : '-'
    ;